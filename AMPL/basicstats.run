printf "\nTax=%d: cost=$%3.2f/MWh ", 
  # Carbon Cost
  carbon_cost,
  # Power Cost
  (Power_Cost - (0
		# Carbon cost adder for new dispatchable projects
		+ sum {(z, t, s, o) in PROJ_DISPATCH, h in TIMEPOINTS}
			DispatchGen[z, t, s, o, h] * (carbon_cost_per_mwh[t, h])
		# Carbon cost adder for existing baseload plants
		+ sum {(z, e, p) in EP_BASELOAD_PERIODS, h in TIMEPOINTS: period[h]=p}
			OperateEPDuringPeriod[z, e, p] * (1-ep_forced_outage_rate[z, e]) * (1-ep_scheduled_outage_rate[z, e]) * ep_size_mw[z, e]
			* (ep_carbon_cost_per_mwh[z, e, h])
		# Carbon cost adder for existing dispatchable projects
		+ sum {(z, e, h) in EP_DISPATCH_HOURS}
			DispatchEP[z, e, h]
			* (ep_carbon_cost_per_mwh[z, e, h])
	)) / system_load_discounted;

printf "\nPercentage of power produced by each fuel in the given investment periods, expressed as a fraction of total power produced.\n";

for {p in PERIODS} {
	printf "%.0d:\t", p;
	let total_power_produced_by_period[p] := (
		# Intermittent
		sum {(z, t, s, o, v, h) in PROJ_INTERMITTENT_VINTAGE_HOURS: period[h]=p }
		   (1-forced_outage_rate[t]) * cap_factor[z, t, s, o, h] * InstallGen[z, t, s, o, v] * hours_in_sample[h]
		+
		# New dispatchable projects
		sum {(z, t, s, o) in PROJ_DISPATCH, h in TIMEPOINTS: period[h]=p } 
		   DispatchGen[z, t, s, o, h] * hours_in_sample[h]
		+
		# Existing dispatchable plants
		sum {(z, e, h) in EP_DISPATCH_HOURS: period[h]=p } 
		   DispatchEP[z, e, h] * hours_in_sample[h]
		+
		# Existing Intermittent plants
		sum {(z, e, h) in EP_INTERMITTENT_OPERATIONAL_HOURS: period[h]=p } 
		   OperateEPDuringPeriod[z, e, period[h]] * ep_size_mw[z, e] * eip_cap_factor[z, e, h] * (1-ep_forced_outage_rate[z, e]) * hours_in_sample[h]
		+
		# New baseload plants
		sum {(z, t, s, o, v, h) in NEW_BASELOAD_VINTAGE_HOURS: period[h]=p} 
		   (1-forced_outage_rate[t]) * (1-scheduled_outage_rate[t]) * InstallGen[z, t, s, o, v] * hours_in_sample[h]
		+ 
		# existing baseload plants
		sum {(z, e, p) in EP_BASELOAD_PERIODS, h in TIMEPOINTS: period[h]=p and p=p} 
		   OperateEPDuringPeriod[z, e, p] * (1-ep_forced_outage_rate[z, e]) * (1-ep_scheduled_outage_rate[z, e]) * ep_size_mw[z, e] * hours_in_sample[h]
		+
		# Hydro
		sum {z in LOAD_AREAS, h in TIMEPOINTS: period[h]=p} (
		   # pumped hydro, de-rated to reflect occasional unavailability of the hydro plants
		   (sum {(z, s) in PROJ_HYDRO} DispatchHydro[z, s, h]) * (1 - forced_outage_rate_hydro) * hours_in_sample[h]
		   - 
		   (sum {(z, s) in PROJ_PUMPED_HYDRO} StorePumpedHydro[z, s, h]) * (1 - forced_outage_rate_hydro) * (1/pumped_hydro_efficiency) * hours_in_sample[h]
		 )
	);
	
	printf { f in FUELS } "%s=%2d\%, ", 
	  f, 
	  100 * (
		# Intermittent
		sum {(z, t, s, o, v, h) in PROJ_INTERMITTENT_VINTAGE_HOURS: period[h]=p and fuel[t] = f}
		   (1-forced_outage_rate[t]) * cap_factor[z, t, s, o, h] * InstallGen[z, t, s, o, v] * hours_in_sample[h]
		+
		# New dispatchable projects
		sum {(z, t, s, o) in PROJ_DISPATCH, h in TIMEPOINTS: period[h]=p and fuel[t] = f} 
		   DispatchGen[z, t, s, o, h] * hours_in_sample[h]
		+
		# Existing dispatchable plants
		sum {(z, e, h) in EP_DISPATCH_HOURS: period[h]=p and ep_fuel[z,e] = f} 
		   DispatchEP[z, e, h] * hours_in_sample[h]
		+
		# Existing Intermittent plants
		sum {(z, e, h) in EP_INTERMITTENT_OPERATIONAL_HOURS: period[h]=p and ep_fuel[z,e] = f} 
		   OperateEPDuringPeriod[z, e, period[h]] * ep_size_mw[z, e] * eip_cap_factor[z, e, h] * (1-ep_forced_outage_rate[z, e]) * hours_in_sample[h]
		+
		# New baseload plants
		sum {(z, t, s, o, v, h) in NEW_BASELOAD_VINTAGE_HOURS: period[h]=p and fuel[t] = f} 
		   (1-forced_outage_rate[t]) * (1-scheduled_outage_rate[t]) * InstallGen[z, t, s, o, v] * hours_in_sample[h]
		+ 
		# existing baseload plants
		sum {(z, e, p) in EP_BASELOAD_PERIODS, h in TIMEPOINTS: period[h]=p and p=p and ep_fuel[z,e] = f} 
		   OperateEPDuringPeriod[z, e, p] * (1-ep_forced_outage_rate[z, e]) * (1-ep_scheduled_outage_rate[z, e]) * ep_size_mw[z, e] * hours_in_sample[h]
		+
		# Hydro
		( if( f = fuel_hydro )
		  then (
			 sum {z in LOAD_AREAS, h in TIMEPOINTS: period[h]=p} (
			   # pumped hydro, de-rated to reflect occasional unavailability of the hydro plants
			   (sum {(z, s) in PROJ_HYDRO} DispatchHydro[z, s, h]) * (1 - forced_outage_rate_hydro) * hours_in_sample[h]
			   - 
			   (sum {(z, s) in PROJ_PUMPED_HYDRO} StorePumpedHydro[z, s, h]) * (1 - forced_outage_rate_hydro) * (1/pumped_hydro_efficiency) * hours_in_sample[h]
			 )
		  )
		  else 0
		)
	  )
	  / total_power_produced_by_period[p]
	  # Add .5% for rounding purposes
	  + .5
	;
	
	printf "Spilled=%2d\%\n", 100 * (total_power_produced_by_period[p] - total_loads_by_period_weighted[p]) / total_power_produced_by_period[p];
	
	printf "Total Load  = %.0d\n", total_loads_by_period_weighted[p];
	printf "Total Power = %.0d\n", total_power_produced_by_period[p];
	
	printf "\n";
}