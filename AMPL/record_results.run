let curtime := time();


shell("if [ -f " & solution_times_path & " ]; then echo 1 > file_exists.txt; else echo 0 > file_exists.txt; fi");
read file_exists < "file_exists.txt";
close "file_exists.txt";
shell("rm file_exists.txt");
if ( file_exists == 0 )
then {
  printf "scenario_id	carbon_cost	process_type time_seconds\n"
  > (solution_times_path);
  close (solution_times_path);
}
# Store solution times
if ( cost_sol_time >= 0 ) then {
	printf "%d	%d	%s	%d\n", scenario_id, carbon_cost, "Cost_Compilation", cost_compile_time  >> (solution_times_path);
	printf "%d	%d	%s	%d\n", scenario_id, carbon_cost, "Cost_Optimization", cost_sol_time  >> (solution_times_path);
	printf "%d	%d	%s	%d\n", scenario_id, carbon_cost, "Transmission_Optimization", trans_sol_time  >> (solution_times_path);
	close (solution_times_path);
}

#######################
# store all hourly generation data in standardized, MW terms
# followed by all hourly storage dispatch decisions

# hourly power production from each source
# as well as total CO2 emissions per hour, heat rate, variable costs per MWh
# storage is included here with a fuel of 'Storage' and power < 0 when storing
let output_section := 'generator_and_storage_dispatch';
let outfile := results_path & sprintf( result_file_path_templates[output_section], carbon_cost );
printf "scenario_id	carbon_cost	period	project_id	load_area_id	load_area	date	hour	technology_id	technology	new	baseload	cogen	storage	fuel	fuel_category	hours_in_sample	power	co2_tons	heat_rate	fuel_cost	carbon_cost_hourly	variable_o_m\n"
  > (outfile);

# New Plants
printf {(pid, a, t) in PROJECTS, h in TIMEPOINTS: ( if not intermittent[t]
		then ProducePowerNonIntermittent[pid, a, t, h]
		else
		(sum {(pid, a, t, install_yr) in PROJECT_VINTAGES: install_yr <= period[h] < project_end_year[t, install_yr] } InstallGen[pid, a, t, install_yr])
			* cap_factor[pid, a, t, h] * ( 1 - forced_outage_rate[t] ) ) != 0} 
  "%s	%d	%d	%d	%d	%s	%d	%d	%d	%s	%d	%d	%d	%d	%s	%s	%d	%.2f	%.2f	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, period[h], pid, load_area_id[a], a, date[h], h,
  technology_id[t], t, 1, new_baseload[t], 0, 0, fuel[t], rps_fuel_category[fuel[t]], hours_in_sample[h],
  ( if not intermittent[t]
		then ProducePowerNonIntermittent[pid, a, t, h]
		else
		(sum {(pid, a, t, install_yr) in PROJECT_VINTAGES: install_yr <= period[h] < project_end_year[t, install_yr] } InstallGen[pid, a, t, install_yr])
			* cap_factor[pid, a, t, h] * ( 1 - forced_outage_rate[t] ) ), 
  Carbon_Emissions[pid, a, t, h],
  heat_rate[t], 
  Fuel_Cost_Incurred_Hourly[pid, a, t, h],
  Carbon_Cost_Incurred_Hourly[pid, a, t, h],
  Variable_Cost_Incurred_Hourly[pid, a, t, h]
  >> (outfile);


# Existing Plants
printf {(a, e, h) in EP_AVAILABLE_HOURS: ProducePowerEP[a, e, h] != 0} 
  "%s	%d	%d	%d	%d	%s	%d	%d	%d	%s	%d	%d	%d	%d	%s	%s	%d	%.2f	%.2f	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, period[h], ep_project_id[a, e], load_area_id[a], a, date[h], h,
  technology_id[ep_technology[a,e]], ep_technology[a,e], 0, ep_baseload[a, e], ep_cogen[a, e], 0, ep_fuel[a, e], rps_fuel_category[ep_fuel[a, e]], hours_in_sample[h],
  ProducePowerEP[a, e, h], 
  EP_Carbon_Emissions[a, e, h],
  ep_heat_rate[a, e], 
  EP_Fuel_Cost_Incurred_Hourly[a, e, h],
  EP_Carbon_Cost_Incurred_Hourly[a, e, h],
  EP_Variable_Cost_Incurred_Hourly[a, e, h]
  >> (outfile);

# Dispatch of non-pumped hydro. 
printf {(a, pid) in PROJ_NONPUMPED_HYDRO, h in TIMEPOINTS, p in PERIODS: period[h]=p and Dispatch_NonPumped_Hydro[a, pid, h] != 0} 
  "%s	%d	%d	%d	%d	%s	%d	%d	%d	%s	%d	%d	%d	%d	%s	%s	%d	%.2f	%.2f	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, pid, load_area_id[a], a, date[h], h,
  hydro_technology_id[a,pid,date[h]], "Hydro_NonPumped", 0, 0, 0, 0, fuel_hydro, rps_fuel_category[fuel_hydro], hours_in_sample[h],
  Dispatch_NonPumped_Hydro[a, pid, h],
  0, 0, 0, 0, 
  NonPumped_Hydro_Variable_Cost_Incurred_Hourly[a, pid, h]
  >> (outfile);

# Dispatch of pumped hydro watershed electrons. 
printf {(a, pid) in PROJ_PUMPED_HYDRO, h in TIMEPOINTS, p in PERIODS: period[h]=p and Dispatch_Pumped_Hydro_Watershed_Electrons[a, pid, h] != 0} 
  "%s	%d	%d	%d	%d	%s	%d	%d	%d	%s	%d	%d	%d	%d	%s	%s	%d	%.2f	%.2f	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, pid, load_area_id[a], a, date[h], h,
  hydro_technology_id[a,pid,date[h]], "Hydro_Pumped", 0, 0, 0, 0, fuel_hydro, rps_fuel_category[fuel_hydro], hours_in_sample[h],
  Dispatch_Pumped_Hydro_Watershed_Electrons[a, pid, h],
  0, 0, 0, 0, 
  0 # Pumped Hydro has 0 Variable O&M costs, as they are wrapped up in Fixed O&M costs
  >> (outfile);


# Store all STORAGE DISPATCH decisions

# Storage of pumped hydro. Don't have to pay variable O&M for storing at the moment.
printf {(a, pid) in PROJ_PUMPED_HYDRO, h in TIMEPOINTS, p in PERIODS, fc in RPS_FUEL_CATEGORY: period[h]=p and Store_Pumped_Hydro[a, pid, h, fc] != 0} 
  "%s	%d	%d	%d	%d	%s	%d	%d	%d	%s	%d	%d	%d	%d	%s	%s	%d	%.2f	%.2f	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, pid, load_area_id[a], a, date[h], h,
  hydro_technology_id[a,pid,date[h]], "Hydro_Pumped", 0, 0, 0, 1, 'Storage', fc, hours_in_sample[h],
  -1 * Store_Pumped_Hydro[a, pid, h, fc],
  0, 0, 0, 0, 0
  >> (outfile);

# Dispatch of stored pumped hydro.
printf {(a, pid) in PROJ_PUMPED_HYDRO, h in TIMEPOINTS, p in PERIODS, fc in RPS_FUEL_CATEGORY: period[h]=p and Dispatch_Pumped_Hydro_Storage[a, pid, h, fc] != 0} 
  "%s	%d	%d	%d	%d	%s	%d	%d	%d	%s	%d	%d	%d	%d	%s	%s	%d	%.2f	%.2f	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, pid, load_area_id[a], a, date[h], h,
  hydro_technology_id[a,pid,date[h]], "Hydro_Pumped", 0, 0, 0, 1, 'Storage', fc, hours_in_sample[h],
  Dispatch_Pumped_Hydro_Storage[a, pid, h, fc],
  0, 0, 0, 0, 0
  >> (outfile);
  
# New storage projects while storing energy. Don't have to pay variable O&M for storing at the moment.
printf {(pid, a, t) in PROJ_STORAGE, h in TIMEPOINTS, p in PERIODS, fc in RPS_FUEL_CATEGORY: period[h]=p and StoreEnergy[pid, a, t, h, fc] != 0} 
  "%s	%d	%d	%d	%d	%s	%d	%d	%d	%s	%d	%d	%d	%d	%s	%s	%d	%.2f	%.2f	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, pid, load_area_id[a], a, date[h], h,
  technology_id[t], t, 1, 0, 0, 1, 'Storage', fc, hours_in_sample[h],
  -1 * StoreEnergy[pid, a, t, h, fc],
  0, 0, 0, 0, 0
  >> (outfile);

# New storage projects while releasing energy.
printf {(pid, a, t) in PROJ_STORAGE, h in TIMEPOINTS, p in PERIODS, fc in RPS_FUEL_CATEGORY: period[h]=p and ReleaseEnergy[pid, a, t, h, fc] != 0} 
  "%s	%d	%d	%d	%d	%s	%d	%d	%d	%s	%d	%d	%d	%d	%s	%s	%d	%.2f	%.2f	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, pid, load_area_id[a], a, date[h], h,
  technology_id[t], t, 1, 0, 0, 1, 'Storage', fc, hours_in_sample[h],
  ReleaseEnergy[pid, a, t, h, fc],
  0, 0, 0, 0,
  ReleaseEnergy[pid, a, t, h, fc] * variable_o_m[pid, a, t]
  >> (outfile);

close (outfile);


#######################
# store all generation CAPACITY data in standardized, MW terms
# (these are quoted as total capacity installed up through each study period)

let output_section := 'generation_capacity';
let outfile := results_path & sprintf( result_file_path_templates[output_section], carbon_cost );
printf "scenario_id	carbon_cost	period	project_id	load_area_id	load_area	technology_id	technology	site	new	baseload	cogen	fuel	capacity	capital_cost	fixed_o_m_cost\n"
> (outfile);

# New Plants, inclusive of new storage projects
printf {(pid, a, t) in PROJECTS, p in PERIODS: (sum {(pid, a, t, install_yr) in PROJECT_VINTAGES: install_yr <= p < project_end_year[t, install_yr] } InstallGen[pid, a, t, install_yr]) > 0}
  "%s	%d	%d	%d	%d	%s	%d	%s	%s	%d	%d	%d	%s	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, pid, load_area_id[a], a, technology_id[t], t, project_location[pid, a, t], 1, new_baseload[t], 0, fuel[t],
  (sum {(pid, a, t, install_yr) in PROJECT_VINTAGES: install_yr <= p < project_end_year[t, install_yr] } InstallGen[pid, a, t, install_yr]),
  Capital_Payments_by_Plant_and_Period[pid, a, t, p ],
  Fixed_OM_by_Plant_and_Period[pid, a, t, p ]
  >> (outfile);

# existing plants (either baseload or dispatchable)
# note: they're only counted as "capacity" if they are operable during this period
# as cogen plants can have their life extended past ep_end_year,
# their capital cost are included here only if they are operated past their normal lifetime (the second printf statement - could probably done in one statement with an in-line if)
printf {(a, e, p) in EP_PERIODS}
  "%s	%d	%d	%d	%d	%s	%d	%s	%s	%d	%d	%d	%s	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, ep_project_id[a, e], load_area_id[a], a, technology_id[ep_technology[a,e]], ep_technology[a,e], e, 0, ep_baseload[a, e], ep_cogen[a, e], ep_fuel[a, e],
  OperateEPDuringPeriod[a, e, p] * ep_size_mw[a, e],
  # Capital costs
  EP_Capital_Payments_by_Plant_and_Period[a, e, p], 
  # Fixed O & M
  EP_Fixed_OM_by_Plant_and_Period[a, e, p]
  >> (outfile);


# hydro plants (non-pumped)
# note: capacity is defined as the maximum possible output on any date in the period
# Fixed costs are based on the hydro_annual_payment_per_mw parameter that is set and explained in windsun.dat. It is reported in terms of re-occurring costs due at the beginning of each study period.
printf {(a, pid) in PROJ_NONPUMPED_HYDRO, p in PERIODS}
  "%s	%d	%d	%d	%d	%s	%d	%s	%s	%d	%d	%d	%s	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, pid, load_area_id[a], a, hydro_technology_id[a,pid,first(DATES)], "Hydro_NonPumped", "Hydro_Agg", 0, 0, 0, fuel_hydro,
  hydro_total_capacity[a, pid],
   # CAPITAL cost
	NonPumped_Hydro_Capital_Payments_by_Plant_and_Period[a, pid, p],
   # fixed O & M cost
 	NonPumped_Hydro_Fixed_OM_by_Plant_and_Period[a, pid, p]
  >> (outfile);

# hydro plants (pumped)
printf {(a, pid) in PROJ_PUMPED_HYDRO, p in PERIODS}
  "%s	%d	%d	%d	%d	%s	%d	%s	%s	%d	%d	%d	%s	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, pid, load_area_id[a], a, hydro_technology_id[a,pid,first(DATES)], "Hydro_Pumped", "Hydro_Agg", 0, 0, 0, fuel_hydro,
  hydro_total_capacity[a, pid],
  # CAPITAL cost
	Pumped_Hydro_Capital_Payments_by_Plant_and_Period[a, pid, p],
  # fixed O & M cost
 	Pumped_Hydro_Fixed_OM_by_Plant_and_Period[a, pid, p]
  >> (outfile);

close (outfile);

########################
# store all trans capacity between zones
let output_section := 'transmission_capacity';
let outfile := results_path & sprintf( result_file_path_templates[output_section], carbon_cost );
printf "scenario_id	carbon_cost	period	transmission_line_id	start_id	end_id	start	end	new	trans_mw	fixed_cost\n"
  > (outfile);

# existing lines
# the cost of the existing transmission grid is calculated in the param transmission_sunk_cost and is not included here
# because it includes all lines, not just the lines between load areas
printf {(a1, a2) in TRANSMISSION_LINES, p in PERIODS}:
  "%s	%d	%d	%d	%d	%d	%s	%s	%d	%.2f	%d\n",
  scenario_id, carbon_cost, p, transmission_line_id[a1, a2], load_area_id[a1], load_area_id[a2], a1, a2, 0,
  existing_transfer_capacity_mw[a1, a2], 0
  >> (outfile);

# new lines
printf {(a1, a2) in TRANSMISSION_LINES, p in PERIODS}:
  "%s	%d	%d	%d	%d	%d	%s	%s	%d	%.2f	%d\n",
  scenario_id, carbon_cost, p, transmission_line_id[a1, a2], load_area_id[a1], load_area_id[a2], a1, a2, 1, 
  sum {install_yr in PERIODS: install_yr <= p < transmission_end_year[install_yr]} InstallTrans[a1, a2, install_yr],
  Transmission_New_Cost_Per_Period[a1, a2, p]
  >> (outfile);

close (outfile);

#######################
# store hourly TRANSMISSION decisions in standardized, MW terms

let output_section := 'transmission_dispatch';
let outfile := results_path & sprintf( result_file_path_templates[output_section], carbon_cost );
printf "scenario_id	carbon_cost	period	transmission_line_id	load_area_receive_id	load_area_from_id	load_area_receive	load_area_from	date	hour	rps_fuel_category	power_sent	power_received	hours_in_sample\n"
  > (outfile);

# detailed imports & exports
let outfile := results_path & sprintf( result_file_path_templates[output_section], carbon_cost );
printf {(x, y) in TRANSMISSION_LINES, h in TIMEPOINTS, fc in RPS_FUEL_CATEGORY: DispatchTransFromXToY[x, y, h, fc] > 0.001} 
  "%s	%d	%d	%d	%d	%d	%s	%s	%d	%d	%s	%.2f	%.2f	%d\n",
  scenario_id, carbon_cost, period[h], transmission_line_id[x, y], load_area_id[y], load_area_id[x], y, x, date[h], h, fc,
  DispatchTransFromXToY[x, y, h, fc],
  DispatchTransFromXToY[x, y, h, fc] * transmission_efficiency[x, y],
  hours_in_sample[h]
  >> (outfile);
close (outfile);

########################
# store the costs of all existing transmission capacity and reduced costs of the RPS constraint
let output_section := 'existing_trans_cost_and_rps_reduced_cost';
let outfile := results_path & sprintf( result_file_path_templates[output_section], carbon_cost );
printf "scenario_id	carbon_cost	period	load_area_id	load_area	existing_trans_cost	rps_reduced_cost\n"
  > (outfile);

if (enable_rps = 1) then {
	printf {a in LOAD_AREAS, p in PERIODS: rps_compliance_fraction_in_period [a, p] > 0 }:
	  "%s	%d	%d	%d	%s	%.2f	%.2f\n",
	  scenario_id, carbon_cost, p, load_area_id[a], a,
	  Transmission_Existing_Cost_Per_Period[a, p],
	  Satisfy_RPS[a, p]
	  >> (outfile);
	printf {a in LOAD_AREAS, p in PERIODS: rps_compliance_fraction_in_period [a, p] = 0 }:
	  "%s	%d	%d	%d	%s	%.2f	%.2f\n",
	  scenario_id, carbon_cost, p, load_area_id[a], a,
	  Transmission_Existing_Cost_Per_Period[a, p],
	  0
	  >> (outfile);
 } else {
	printf {a in LOAD_AREAS, p in PERIODS}:
	  "%s	%d	%d	%d	%s	%.2f	%.2f\n",
	  scenario_id, carbon_cost, p, load_area_id[a], a,
	  Transmission_Existing_Cost_Per_Period[a, p],
	  0
	  >> (outfile);
}

close (outfile);

########################
# store local T&D capacity within each zone
let output_section := 'local_td_capacity';
let outfile := results_path & sprintf( result_file_path_templates[output_section], carbon_cost );
printf "scenario_id	carbon_cost	period	load_area_id	load_area	new local_td_mw	fixed_cost\n"
  > (outfile);

# existing local T&D - assumed to be equal to the planning reserve margin above max_coincident_load_for_local_td 
printf {a in LOAD_AREAS, p in PERIODS}:
  "%s	%d	%d	%d	%s	%d	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, load_area_id[a], a, 0,
  existing_local_td[a],
  Local_TD_Existing_Cost_Per_Period[a, p]
  >> (outfile);

# newly installed local T&D
printf {a in LOAD_AREAS, p in PERIODS}:
  "%s	%d	%d	%d	%s	%d	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, load_area_id[a], a, 1,
  sum {install_yr in PERIODS: install_yr <= p < local_td_end_year[install_yr]} InstallLocalTD[a, install_yr],
  Local_TD_New_Cost_Per_Period[a, p]
  >> (outfile);

close (outfile);

#######################
# system LOAD
let output_section := 'system_load';
let outfile := results_path & sprintf( result_file_path_templates[output_section], carbon_cost );
printf "scenario_id	carbon_cost	period	load_area_id	load_area	date	hour	hours_in_sample	power	satisfy_load_reduced_cost	satisfy_load_reserve_reduced_cost\n"
  > (outfile);

printf {a in LOAD_AREAS, h in TIMEPOINTS, p in PERIODS: period[h]=p} 
  "%s	%d	%d	%d	%s	%d	%d	%d	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, load_area_id[a], a, date[h], h, hours_in_sample[h], system_load[a, h], Satisfy_Load[a, h], Satisfy_Load_Reserve[a, h]
  >> (outfile);
close (outfile);


printf "%d seconds to store results.\n", time()-curtime;
