let curtime := time();


shell("if [ -f " & solution_times_path & " ]; then echo 1 > file_exists.txt; else echo 0 > file_exists.txt; fi");
read file_exists < "file_exists.txt";
close "file_exists.txt";
shell("rm file_exists.txt");
if ( file_exists == 0 )
then {
  printf "scenario_id	carbon_cost	cost_solution_seconds	trans_solution_seconds\n"
  > (solution_times_path);
}
#######################
# Store solution times
printf "%s	%d	%d	%d\n", scenario_id, carbon_cost, cost_sol_time, trans_sol_time
  >> (solution_times_path);


#######################
# store all hourly generation data in standardized, MW terms

# hourly power production from each source
# as well as total CO2 emissions per hour, heat rate, variable costs per MWh
# It might be better to report sub-components (CO2_per_mwh, variable_o_m and fuel_cost_hourly) instead of total emissions and variable costs
# (that would simplify the code here, but then the components would have to be multiplied and added in mysql later)
let output_section := 'dispatch';
let outfile := result_header_paths[output_section];
printf "scenario_id	carbon_cost	period	project_id	load_area_id	load_area	date	hour	technology_id	technology	location_id	new	baseload	cogen	fuel	power	co2_tons	hours_in_sample	heat_rate	fuel_cost_tot	carbon_cost_tot	variable_o_m_tot\n"
  > (outfile);
close (outfile);

# new dispatchable projects
let outfile := sprintf( result_file_path_templates[output_section], carbon_cost );
#"results/dispatch_" & carbon_cost & ".txt";
printf {(pid, a, t) in PROJ_DISPATCH, h in TIMEPOINTS} 
  "%s	%.2f	%d	%d	%d	%s	%d	%d	%d	%s	%s	%d	%d	%d	%s	%.2f	%.2f	%d	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, period[h], pid, load_area_id[a], a, date[h], h, technology_id[t], t, location_unspecified, 1, 0, 0, fuel[t], 
  DispatchGen[pid, a, t, h], 
  DispatchGen[pid, a, t, h] * heat_rate[t] * carbon_content[fuel[t]],
  hours_in_sample[h],
  heat_rate[t], 
  DispatchGen[pid, a, t, h] * heat_rate[t] * fuel_cost_hourly[a, fuel[t], h],
  DispatchGen[pid, a, t, h] * heat_rate[t] * carbon_content[fuel[t]] * carbon_cost,
  DispatchGen[pid, a, t, h] * variable_o_m[pid, a, t]
  > (outfile);


# new intermittent projects (all renewables, so no carbon emissions)
printf {(pid, a, t, h) in PROJ_INTERMITTENT_HOURS}
  "%s	%.2f	%d	%d	%d	%s	%d	%d	%d	%s	%s	%d	%d	%d	%s	%.2f	%.0f	%d	%.0f	%.0f	%.0f	%.2f\n",
  scenario_id, carbon_cost, period[h], pid, load_area_id[a], a, date[h], h, technology_id[t], t, project_location[pid, a, t], 1, 0, 0, fuel[t], 
  (sum {(pid, a, t, install_yr, h) in PROJ_INTERMITTENT_VINTAGE_HOURS} 
    (1-forced_outage_rate[t]) * cap_factor[pid, a, t, h] * InstallGen[pid, a, t, install_yr]),
  0,
  hours_in_sample[h],
  0, 
  0, 0,
  (sum {(pid, a, t, install_yr, h) in PROJ_INTERMITTENT_VINTAGE_HOURS} 
    (1-forced_outage_rate[t]) * cap_factor[pid, a, t, h] * InstallGen[pid, a, t, install_yr]) * variable_o_m[pid, a, t]
  >> (outfile);

# new baseload plants
# 
printf {(pid, a, t) in setof{(pid, a, t, install_yr) in PROJECT_VINTAGES} (pid, a, t), h in TIMEPOINTS}
  "%s	%.2f	%d	%d	%d	%s	%d	%d	%d	%s	%s	%d	%d	%d	%s	%.2f	%.2f	%d	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, period[h], pid, load_area_id[a], a, date[h], h, technology_id[t], t, location_unspecified, 1, new_baseload[t], 0, fuel[t],
  (sum{(pid, a, t, install_yr, h) in NEW_BASELOAD_VINTAGE_HOURS} 
    (1-forced_outage_rate[t]) * (1-scheduled_outage_rate[t]) * InstallGen[pid, a, t, install_yr]),
  (sum{(pid, a, t, install_yr, h) in NEW_BASELOAD_VINTAGE_HOURS} 
    (1-forced_outage_rate[t]) * (1-scheduled_outage_rate[t]) * InstallGen[pid, a, t, install_yr]) 
    * heat_rate[t] * carbon_content[fuel[t]],
  hours_in_sample[h],
  heat_rate[t], 
  (sum{(pid, a, t, install_yr, h) in NEW_BASELOAD_VINTAGE_HOURS} 
    (1-forced_outage_rate[t]) * (1-scheduled_outage_rate[t]) * InstallGen[pid, a, t, install_yr]) 
    * heat_rate[t] * fuel_cost_hourly[a,fuel[t], h],
  (sum{(pid, a, t, install_yr, h) in NEW_BASELOAD_VINTAGE_HOURS} 
    (1-forced_outage_rate[t]) * (1-scheduled_outage_rate[t]) * InstallGen[pid, a, t, install_yr]) 
    * heat_rate[t] * carbon_content[fuel[t]] * carbon_cost,
  (sum{(pid, a, t, install_yr, h) in NEW_BASELOAD_VINTAGE_HOURS} 
    (1-forced_outage_rate[t]) * (1-scheduled_outage_rate[t]) * InstallGen[pid, a, t, install_yr])
    * variable_o_m[pid, a, t]
  >> (outfile);

# Existing Intermittent plants
# 
printf {(a, e, h) in EP_INTERMITTENT_OPERATIONAL_HOURS, p in PERIODS: period[h]=p} 
  "%s	%.2f	%d	%d	%d	%s	%d	%d	%d	%s	%s	%d	%d	%d	%s	%.2f	%.2f	%d	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, ep_project_id[a, e], load_area_id[a], a, date[h], h, technology_id[ep_technology[a,e]], ep_technology[a,e], e, 0, ep_baseload[a, e], ep_cogen[a, e], ep_fuel[a, e],
  # Power
  OperateEPDuringPeriod[a, e, p] * ep_size_mw[a, e] * eip_cap_factor[a, e, h] * (1-ep_forced_outage_rate[a, e]), 
  # Co2 (tons)
  OperateEPDuringPeriod[a, e, p] * ep_size_mw[a, e] * eip_cap_factor[a, e, h] * (1-ep_forced_outage_rate[a, e])
    * ep_heat_rate[a, e] * carbon_content[ep_fuel[a, e]],
  # hours_in_sample
  hours_in_sample[h],
  # heat_rate
  ep_heat_rate[a, e], 
  # fuel_cost_tot
  OperateEPDuringPeriod[a, e, p] * ep_size_mw[a, e] * eip_cap_factor[a, e, h] * (1-ep_forced_outage_rate[a, e])
    * ep_heat_rate[a, e] * fuel_cost_hourly[a,ep_fuel[a, e], h],
  # carbon_cost_tot
  OperateEPDuringPeriod[a, e, p] * ep_size_mw[a, e] * eip_cap_factor[a, e, h] * (1-ep_forced_outage_rate[a, e])
    * ep_heat_rate[a, e] * carbon_content[ep_fuel[a, e]] * carbon_cost,
  # variable_o_m_tot
  OperateEPDuringPeriod[a, e, p] * ep_size_mw[a, e] * eip_cap_factor[a, e, h] * (1-ep_forced_outage_rate[a, e])
    * ep_variable_o_m[a, e]
  >> (outfile);


# existing baseload plants
printf {(a, e, p) in EP_BASELOAD_PERIODS, h in TIMEPOINTS: period[h]=p} 
  "%s	%.2f	%d	%d	%d	%s	%d	%d	%d	%s	%s	%d	%d	%d	%s	%.2f	%.2f	%d	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, ep_project_id[a, e], load_area_id[a], a, date[h], h, technology_id[ep_technology[a,e]], ep_technology[a,e], e, 0, ep_baseload[a, e], ep_cogen[a, e], ep_fuel[a, e],
  OperateEPDuringPeriod[a, e, p] * (1-ep_forced_outage_rate[a, e]) * (1-ep_scheduled_outage_rate[a, e]) * ep_size_mw[a, e],
  OperateEPDuringPeriod[a, e, p] * (1-ep_forced_outage_rate[a, e]) * (1-ep_scheduled_outage_rate[a, e]) * ep_size_mw[a, e] 
    * ep_heat_rate[a, e] * carbon_content[ep_fuel[a, e]],
  hours_in_sample[h],
  ep_heat_rate[a, e], 
  OperateEPDuringPeriod[a, e, p] * (1-ep_forced_outage_rate[a, e]) * (1-ep_scheduled_outage_rate[a, e]) * ep_size_mw[a, e] 
    * ep_heat_rate[a, e] * fuel_cost_hourly[a,ep_fuel[a, e], h],
  OperateEPDuringPeriod[a, e, p] * (1-ep_forced_outage_rate[a, e]) * (1-ep_scheduled_outage_rate[a, e]) * ep_size_mw[a, e] 
    * ep_heat_rate[a, e] * carbon_content[ep_fuel[a, e]] * carbon_cost,
  OperateEPDuringPeriod[a, e, p] * (1-ep_forced_outage_rate[a, e]) * (1-ep_scheduled_outage_rate[a, e]) * ep_size_mw[a, e]
    * ep_variable_o_m[a, e]
  >> (outfile);

# existing dispatchable plants
printf {(a, e, h) in EP_DISPATCH_HOURS, p in PERIODS: p=period[h]} 
  "%s	%.2f	%d	%d	%d	%s	%d	%d	%d	%s	%s	%d	%d	%d	%s	%.2f	%.2f	%d	%.2f	%.2f	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, ep_project_id[a, e], load_area_id[a], a, date[h], h, technology_id[ep_technology[a,e]], ep_technology[a,e], e, 0, ep_baseload[a, e], ep_cogen[a, e], ep_fuel[a, e],
  DispatchEP[a, e, h],
  DispatchEP[a, e, h] * ep_heat_rate[a, e] * carbon_content[ep_fuel[a, e]],
  hours_in_sample[h],
  ep_heat_rate[a, e], 
  DispatchEP[a, e, h]
    * ep_heat_rate[a, e] * fuel_cost_hourly[a, ep_fuel[a, e], h],
  DispatchEP[a, e, h]
    * ep_heat_rate[a, e] * carbon_content[ep_fuel[a, e]] * carbon_cost,
  DispatchEP[a, e, h]
    * ep_variable_o_m[a, e]
  >> (outfile);

# Dispatch of non-pumped hydro. In the results database, Hydro has a hard-coded id of 15, and Hydro Pumping has 16 (see DatabasePrep/GeneratorInfo/generator_costs.csv)
printf {(a, proj) in PROJ_NONPUMPED_HYDRO, h in TIMEPOINTS, p in PERIODS: period[h]=p} 
  "%s	%.2f	%d	%d	%d	%s	%d	%d	%d	%s	%s	%d	%d	%d	%s	%.2f	%.0f	%d	%.0f	%.0f	%.0f	%.0f\n",
  scenario_id, carbon_cost, p, proj, load_area_id[a], a, date[h], h, 15, "Hydro_NonPumped", hydro_site[a, proj, first(DATES)], 0, 0, 0,
  fuel_hydro, (1 - forced_outage_rate_hydro) * DispatchHydro[a, proj, h],
  0, hours_in_sample[h], 0, 
  0, 0, 0
  >> (outfile);

# Dispatch of pumped hydro. 
printf {(a, proj) in PROJ_PUMPED_HYDRO, h in TIMEPOINTS, p in PERIODS: period[h]=p} 
  "%s	%.2f	%d	%d	%d	%s	%d	%d	%d	%s	%s	%d	%d	%d	%s	%.2f	%.0f	%d	%.0f	%.0f	%.0f	%.0f\n",
  scenario_id, carbon_cost, p, proj, load_area_id[a], a, date[h], h, 16, "Hydro_Pumped", hydro_site[a, proj, first(DATES)], 0, 0, 0,
  fuel_hydro, (1 - forced_outage_rate_hydro) * DispatchHydro[a, proj, h],
  0, hours_in_sample[h], 0, 
  0, 0, 0
  >> (outfile);

# Storage of pumped hydro.
printf {(a, proj) in PROJ_PUMPED_HYDRO, h in TIMEPOINTS, p in PERIODS: period[h]=p} 
  "%s	%.2f	%d	%d	%d	%s	%d	%d	%d	%s	%s	%d	%d	%d	%s	%.2f	%.0f	%d	%.0f	%.0f	%.0f	%.0f\n",
  scenario_id, carbon_cost, p, proj, load_area_id[a], a, date[h], h, 16, "Hydro_Pumped", hydro_site[a, proj, first(DATES)], 0, 0, 0, fuel_hydro,
  -1 * (1 - forced_outage_rate_hydro) * (1/pumped_hydro_efficiency) * StorePumpedHydro[a, proj, h],
  0, hours_in_sample[h], 0, 
  0, 0, 0
  >> (outfile);

close (outfile);



#######################
# system load
let output_section := 'system_load';
let outfile := result_header_paths[output_section];
printf "scenario_id	carbon_cost	period	load_area_id	load_area	date	hour	power	hours_in_sample\n"
  > (outfile);
close (outfile);
let outfile := sprintf( result_file_path_templates[output_section], carbon_cost );
printf {a in LOAD_AREAS, h in TIMEPOINTS, p in PERIODS: period[h]=p} 
  "%s	%.2f	%d	%d	%s	%d	%d	%.2f	%d\n",
  scenario_id, carbon_cost, p, load_area_id[a], a, date[h], h, system_load[a, h], hours_in_sample[h]
  > (outfile);
close (outfile);



#######################
# store detailed transmission info in standardized, MW terms

let output_section := 'transmission';
let outfile := result_header_paths[output_section];
printf "scenario_id	carbon_cost	period	load_area_receive_id	load_area_from_id	load_area_receive	load_area_from	date	hour	rps_fuel_category	power_sent	power_received	hours_in_sample\n"
  > (outfile);
close (outfile);


# detailed imports & exports
let outfile := sprintf( result_file_path_templates[output_section], carbon_cost );
printf {(x, y) in TRANSMISSION_LINES, h in TIMEPOINTS, fuel_cat in RPS_FUEL_CATEGORY: DispatchTransFromXToY[x, y, h, fuel_cat] > 0.001} 
  "%s	%.2f	%d	%d	%d	%s	%s	%d	%d	%s	%.2f	%.2f	%d\n",
  scenario_id, carbon_cost, period[h], load_area_id[y], load_area_id[x], y, x, date[h], h, fuel_cat,
  DispatchTransFromXToY[x, y, h, fuel_cat],
  DispatchTransFromXToY[x, y, h, fuel_cat] * transmission_efficiency[x, y],
  hours_in_sample[h]
  > (outfile);
close (outfile);


#######################
# store all gen/trans capacity data in standardized, MW terms
# (these are quoted as total capacity installed up through each study period)

let output_section := 'generation_capacity';
let outfile := result_header_paths[output_section];
printf "scenario_id	carbon_cost	period	project_id	load_area_id	load_area	technology_id	technology	site	new	baseload	cogen	fuel	capacity	fixed_cost\n" > (outfile);
close (outfile);

let outfile := sprintf( result_file_path_templates[output_section], carbon_cost );
# new projects INTERMITTENT
printf {(pid, a, t) in PROJ_INTERMITTENT union PROJ_RESOURCE_LIMITED, p in PERIODS: (max {(pid, a, t, install_yr) in PROJECT_VINTAGES} InstallGen[pid, a, t, install_yr]) > 0}
  "%s	%.2f	%d	%d	%d	%s	%d	%s	%s	%d	%d	%d	%s	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, pid, load_area_id[a], a, technology_id[t], t, project_location[pid, a, t], 1, new_baseload[t], 0, fuel[t],
  sum {(pid, a, t, install_yr) in PROJECT_VINTAGES: install_yr <= p < project_end_year[t, install_yr]} InstallGen[pid, a, t, install_yr],
  sum {(pid, a, t, install_yr) in PROJECT_VINTAGES: install_yr <= p < project_end_year[t, install_yr]} InstallGen[pid, a, t, install_yr]
    * (capital_cost_annual_payment[pid, a, t,install_yr] + fixed_o_m[pid, a, t]) * (1-(1/(1+discount_rate)^(num_years_per_period)))/discount_rate
  > (outfile);

# new projects DISPATCHABLE & BASELOAD
printf {(pid, a, t) in PROJ_ANYWHERE, p in PERIODS: (max {(pid, a, t, install_yr) in PROJECT_VINTAGES} InstallGen[pid, a, t, install_yr]) > 0}
  "%s	%.2f	%d	%d	%d	%s	%d	%s	%s	%d	%d	%d	%s	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, pid, load_area_id[a], a, technology_id[t], t, location_unspecified, 1, new_baseload[t], 0, fuel[t],
  sum {(pid, a, t, install_yr) in PROJECT_VINTAGES: install_yr <= p < project_end_year[t, install_yr]} InstallGen[pid, a, t, install_yr],
  sum {(pid, a, t, install_yr) in PROJECT_VINTAGES: install_yr <= p < project_end_year[t, install_yr]} InstallGen[pid, a, t, install_yr]
    * (capital_cost_annual_payment[pid, a, t,install_yr] + fixed_o_m[pid, a, t]) * (1-(1/(1+discount_rate)^(num_years_per_period)))/discount_rate
  > (outfile);


# existing plants (either baseload or dispatchable)
# note: they're only counted as "capacity" if they are operable during this period
printf {(a, e, p) in EP_PERIODS}
  "%s	%.2f	%d	%d	%d	%s	%d	%s	%s	%d	%d	%d	%s	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, ep_project_id[a, e], load_area_id[a], a, technology_id[ep_technology[a,e]], ep_technology[a,e], e, 0, ep_baseload[a, e], ep_cogen[a, e], ep_fuel[a, e],
  OperateEPDuringPeriod[a, e, p] * ep_size_mw[a, e],
  OperateEPDuringPeriod[a, e, p] * ep_size_mw[a, e] * ep_fixed_cost[a, e, p] * (1+discount_rate)^(p-base_year)
  + ep_size_mw[a, e] * ep_capital_cost_annual_payment[a, e] * (1-(1/(1+discount_rate)^(num_years_per_period)))/discount_rate
  >> (outfile);

# hydro plants (non-pumped)
# note: capacity is defined as the maximum possible output on any date in the period
# Fixed costs are based on the hydro_annual_payment_per_mw parameter that is set and explained in windsun.dat. It is reported in terms of re-occurring costs due at the beginning of each study period.
# In the results database, Hydro has a hard-coded id of 15, and Hydro Pumping has 16 (see DatabasePrep/GeneratorInfo/generator_costs.csv)
printf {(a, proj) in PROJ_NONPUMPED_HYDRO, p in PERIODS}
  "%s	%.2f	%d	%d	%d	%s	%d	%s	%s	%d	%d	%d	%s	%.2f	%.0f\n",
  scenario_id, carbon_cost, p, proj, load_area_id[a], a, 15, "Hydro_NonPumped", hydro_site[a, proj, first(DATES)], 0, 0, 0, fuel_hydro,
  max {d in DATES, h in TIMEPOINTS: date[h] = d and period[h] = p} max_hydro_flow[a, proj, d],
  # Estimate of MW installed
  (max {(a, proj, d) in PROJ_HYDRO_DATES} max_hydro_flow[a, proj, d])
  # Regional annual costs per MW
  * hydro_annual_payment_per_mw * economic_multiplier[a]
  # Converts annual payments over the study period to a single payment at the beginning of the study period
  * (1-(1/(1+discount_rate)^(num_years_per_period)))/discount_rate
  >> (outfile);
# hydro plants (pumped)
printf {(a, proj) in PROJ_PUMPED_HYDRO, p in PERIODS}
  "%s	%.2f	%d	%d	%d	%s	%d	%s	%s	%d	%d	%d	%s	%.2f	%.0f\n",
  scenario_id, carbon_cost, p, proj, load_area_id[a], a, 16, "Hydro_Pumped", hydro_site[a, proj, first(DATES)], 0, 0, 0, fuel_hydro,
  max {d in DATES, h in TIMEPOINTS: date[h] = d and period[h] = p} max_hydro_flow[a, proj, d],
  # Estimate of MW installed
  (max {(a, proj, d) in PROJ_HYDRO_DATES} max_hydro_flow[a, proj, d])
  # Regional annual costs per MW
  * hydro_annual_payment_per_mw * economic_multiplier[a]
  # Converts annual payments over the study period to a single payment at the beginning of the study period
  * (1-(1/(1+discount_rate)^(num_years_per_period)))/discount_rate
  >> (outfile);

close (outfile);

########################
# store all trans capacity between zones
let output_section := 'transmission_capacity';
let outfile := result_header_paths[output_section];
printf "scenario_id	carbon_cost	period	start_id	end_id	start	end	transmission_line_id	new	trans_mw	fixed_cost\n" > (outfile);
close (outfile);

let outfile := sprintf( result_file_path_templates[output_section], carbon_cost );
# existing lines 
# TODO: update this if the main model is changed to retire existing lines when they reach transmission_max_age_years
# TODO: switch this to record the bidirectional capacities, instead of averaging them together
printf {(z1, z2) in TRANSMISSION_LINES, p in PERIODS}:
  "%s	%.2f	%d	%d	%d	%s	%s	%d	%d	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, load_area_id[z1], load_area_id[z2], z1, z2, transmission_line_id[z1, z2], 0, (existing_transfer_capacity_mw[z1, z2]),
  ((existing_transfer_capacity_mw[z1, z2])) 
    * transmission_annual_payment[z1, z2, first(PERIODS)] * (1-(1/(1+discount_rate)^(num_years_per_period)))/discount_rate
  > (outfile);

# new lines
printf {(z1, z2) in TRANSMISSION_LINES, p in PERIODS}:
  "%s	%.2f	%d	%d	%d	%s	%s	%d	%d	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, load_area_id[z1], load_area_id[z2], z1, z2, transmission_line_id[z1, z2], 1, 
  sum {install_yr in PERIODS: install_yr <= p < transmission_end_year[install_yr]} InstallTrans[z1, z2, install_yr],
  (sum {install_yr in PERIODS: install_yr <= p < transmission_end_year[install_yr]} InstallTrans[z1, z2, install_yr] * transmission_annual_payment[z1, z2, install_yr])
     * (1-(1/(1+discount_rate)^(num_years_per_period)))/discount_rate
  >> (outfile);

close (outfile);


########################
# store local T&D capacity within each zone
let output_section := 'local_trans_and_distribution_capacity';
let outfile := result_header_paths[output_section];
printf "scenario_id	carbon_cost	period	load_area_id	load_area	local_td_mw	fixed_cost\n" > (outfile);
close (outfile);

let outfile := sprintf( result_file_path_templates[output_section], carbon_cost );
printf {p in PERIODS, a in LOAD_AREAS}:
  "%s	%.2f	%d	%d	%s	%.2f	%.2f\n",
  scenario_id, carbon_cost, p, load_area_id[a], a, 
  sum {install_yr in PERIODS: install_yr <= p < transmission_end_year[install_yr]} InstallLocalTD[a, install_yr],
  (sum {install_yr in PERIODS: install_yr <= p < transmission_end_year[install_yr]} InstallLocalTD[a, install_yr])
    * local_td_annual_payment_per_mw * (1-(1/(1+discount_rate)^(num_years_per_period)))/discount_rate
  > (outfile);

close (outfile);

printf "%d seconds to store results.\n", time()-curtime;
